If pVermelden     ;; opbouwen ivSelectieTekst
	Calculate lib as pick(len(pLib)>0,ivLib,pLib)
	Calculate tabelkolom as pick(len(pTabelKolom2)>0,pTabelKolom,pTabelKolom2)
	Calculate inhoud as pick(len(pInhoud2)>0,pInhoud,pInhoud2)
	If pos('.',tabelkolom)
		Calculate schema as mid(tabelkolom,1,pos('.',tabelkolom)-1)
		Calculate kolom as mid(tabelkolom,pos('.',tabelkolom)+1,len(tabelkolom))
	Else
		Calculate schema as ivTableName
		Calculate kolom as tabelkolom
	End If
	Calculate desc as $libs.[lib].$schemas.[schema].$objs.[kolom].$desc
	Calculate desc as replaceall(desc,'[','')
	Calculate desc as replaceall(desc,']','')
	Calculate desc as replaceall(desc,'*','')
	Test for valid calculation {eval(desc)}
	If flag true
		Calculate desc as eval(desc)
	End If
	Calculate ivSelectieTekst as con(ivSelectieTekst,desc,' ',pOperator,' ',inhoud,',')
End If

If (pOperator='is null')|(pOperator='is not null')     ;; is null operator
	Do ivQueryList.$add(con(pTabelKolom,' ',pOperator))
	Quit method kTrue
End If
If pOperator='is empty'     ;; empty
	Do ivQueryList.$add(con(pTabelKolom,'=',"''"))
	Quit method kTrue
End If

If (pOperator='like')|(pOperator='not like')
	Calculate pInhoud as con(pInhoud,'%')
End If

Switch pDatatype
	Case "N"
		Calculate pInhoud as replaceall(pInhoud,',','.')
	Case 'C'
		If pos("'",pInhoud)
			If pOperator='like'
				Calculate pInhoud as replaceall(pInhoud,"'","_")     ;; O'NEILL
			Else
				Calculate pInhoud as replaceall(pInhoud,"'","''")     ;; Evenementenbureau's
			End If
		End If
		Calculate pInhoud as con("'",pInhoud,"'")
	Case "D"
		Calculate datum as pInhoud
		Calculate pInhoud as con("DATE '",dat(datum,'y-M-D'),"'")
	Case "T"
		Calculate pInhoud as con("TIME '",jst(pInhoud,'T:H:N:S'),"'")
	Case "DT"
		Calculate datumtijd as pInhoud
		Calculate pInhoud as con("TIMESTAMP '",dat(datumtijd,'y-M-D')," ",jst(datumtijd,'T:H:N:S'),"'")
	Case "B"
		Calculate Boolean as pInhoud
		If Boolean
			Calculate pInhoud as 'true'
		Else
			Calculate pInhoud as 'false'
		End If
	Default
End Switch

Do ivQueryList.$add(con(pTabelKolom,' ',pOperator,' ',pInhoud))
Quit method kTrue